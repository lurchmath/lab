include libs/Recursion
////////////////////////////////////////////////////////////////////
// Recursive Sequences Test Proofs
// 

➤ "Math 299 Recursive Sequences Homework Proofs 2023"

{ // wrap them all
  
➤ "Just a warm up example (not assigned)"

{
  ➤ "Theorem: Sum(i^2,i,2) = 2^2+(1^2+0^2)"
	{ Sum(i^2,i,2) = 2^2+(1^2+0^2) }
	➤ "Proof:"
	{
		Sum(i^2,i,2) = Sum(i^2,i,𝜎(1))
							 = (𝜎(1))^2+Sum(i^2,i,1)
							 = 2^2+Sum(i^2,i,1)
							 = 2^2+Sum(i^2,i,𝜎(0))
							 = 2^2+((𝜎(0))^2+Sum(i^2,i,0))
							 = 2^2+(1^2+Sum(i^2,i,0))
							 = 2^2+(1^2+0^2)
	}
}

➤ "Thm 8.2(c) - Assignment #15"
➤
➤ "We are allowed to use 8.2(b), proved in class"
➤
Rule: :{ z^m⋅z^n = z^(m+n) }

{

  ➤ "Theorem: (z^m)^n=z^(m⋅n)"
	{ (z^m)^n=z^(m⋅n) }
	➤ "Proof: (by induction on n for arbitrary m)"
	{
		➤ "Base case:"
		(z^m)^0 = 1
		        = z^0
						= z^(m⋅0)
		➤ "Inductive step:"
		{ :Let k be such that (z^m)^k = z^(m⋅k)

			(z^m)^(𝜎(k)) = z^m⋅(z^m)^k
			             = z^m⋅z^(m⋅k)
								   = z^(m+m⋅k)
								   = z^(m⋅𝜎(k))

		}				
		➤ "Therefore, by induction"
		∀n.(z^m)^n=z^(m⋅n)
		➤ "and the theorem follows."
	}
}

➤ "Thm 8.5(a) - Assignment #15"

{
	➤ "Theorem: Sum(a(i,i,n)+b(i)) = Sum(a(i,i,n))+Sum(b(i,i,n))"
	{ Sum((a(i)+b(i)),i,n) = Sum(a(i),i,n) + Sum(b(i),i,n) }
	➤ "Proof: (by induction on n)"
	{
    ➤ "Base Case"
		  Sum((a(i)+b(i)),i,0) = a(0)+b(0)
			                 = Sum(a(i),i,0)+b(0)
											 = Sum(a(i),i,0)+Sum(b(i),i,0)
     
		➤ "Inductive step:"
		{ :Let k be such that Sum((a(i)+b(i)),i,k) = Sum(a(i),i,k)+Sum(b(i),i,k)
			
			Sum((a(i)+b(i)),i,𝜎(k)) 
			        = ( a(𝜎(k))+b(𝜎(k)) ) + Sum((a(i)+b(i)),i,k) 
			        = ( a(𝜎(k))+b(𝜎(k)) ) + ( Sum(a(i),i,k)+Sum(b(i),i,k) )
			        =  a(𝜎(k)) + (b(𝜎(k))+( Sum(a(i),i,k)+Sum(b(i),i,k) ))
			        =  a(𝜎(k)) + ( ( b(𝜎(k))+Sum(a(i),i,k) ) + Sum(b(i),i,k) )
			        =  a(𝜎(k)) + ( ( Sum(a(i),i,k)+b(𝜎(k)) ) + Sum(b(i),i,k) )
			        =  a(𝜎(k)) + ( Sum(a(i),i,k)+ (b(𝜎(k))+Sum(b(i),i,k)) )
			        =  (a(𝜎(k)) + Sum(a(i),i,k)) + (b(𝜎(k))+Sum(b(i),i,k))
			        =  Sum(a(i),i,𝜎(k)) + (b(𝜎(k))+Sum(b(i),i,k))
			        =  Sum(a(i),i,𝜎(k)) + Sum(b(i),i,𝜎(k))
		}
		➤ "Therefore, by induction"
		∀n.Sum((a(i)+b(i)),i,n) = Sum(a(i),i,n) + Sum(b(i),i,n)
		➤ "and the theorem follows."
	}
}

➤ "Thm 8.7 - Assignment #15 - Fibonacci numbers"

{
  ➤ "Theorem: Fib(n+3)+Fib(n)=2⋅Fib(n+2)"
	{ Fib(n+3)+Fib(n)=2⋅Fib(n+2) }
	➤ "Proof:"
	{
		Fib(n+3)+Fib(n) = Fib(n+𝜎(2))+Fib(n)
		            = Fib(n+(2+1))+Fib(n)
		            = Fib(n+(1+2))+Fib(n)
		            = Fib((n+1)+2)+Fib(n)
								= (Fib((n+1)+1)+Fib(n+1))+Fib(n)
								= (Fib(n+(1+1))+Fib(n+1))+Fib(n)
								= (Fib(n+𝜎(1))+Fib(n+1))+Fib(n)
								= (Fib(n+2)+Fib(n+1))+Fib(n)
								= Fib(n+2)+(Fib(n+1)+Fib(n))
								= Fib(n+2)+Fib(n+2)
								= 1⋅Fib(n+2)+Fib(n+2)
								= 1⋅Fib(n+2)+1⋅Fib(n+2)
								= (1+1)⋅Fib(n+2)
								= 𝜎(1)⋅Fib(n+2)
		            = 2⋅Fib(n+2)
	}
} ✔︎

// end of all
} 