include libs/Equations
////////////////////////////////////////////////////////////////////
// Peano Test Proofs
//
//
{ // wrap them all
  
  ➤ "Math 299 Peano Axiom Homework Proofs 2023"

{
  ➤ "Theorem: 1+1=2"
  { 1+1=2 }
  
  ➤ "Proof:"
  Proof:
  {  
    1+1=1+𝜎(0)
       =𝜎(1+0)
       =𝜎(1)
       =2
  }  
}

{
	Rule: :{ n⋅1 = n }
	
	➤ "Theorem: 2⋅2=4"
  { 2⋅2=4 }
 	{
	2⋅2 = 2⋅𝜎(1)
	    = 2+2⋅1
	    = 2+2
	    = 2+𝜎(1)
	    = 𝜎(2+1)
	    = 𝜎(2+𝜎(0))
	    = 𝜎(𝜎(2+0))
	    = 𝜎(𝜎(2))
	    = 𝜎(3)
	    = 4
	}
}

➤ "Theorem 7.2: n≠0 ⇒ ∃m,n=𝜎(m)"
Theorem: { :n≠0 ∃m.n=𝜎(m) }
// Rule: { :n≠0 ∃m.n=𝜎(m) }

{
  ➤ "Proof:"
  Proof:
  { :n≠0
    ➤ "P(n) is n≠0⇒∃m,n=𝜎(m)"
    ➤ "Base Case"
    { :¬0=0
      { :¬(∃m.0=𝜎(m))
        0=0
        →←
      }
      ∃m.0=𝜎(m)
    }
    0≠0 ⇒ (∃m.0=𝜎(m))
    ➤ "Inductive step"
    { :Let k be such that ¬k=0 ⇒ (∃m.k=𝜎(m))
      { :𝜎(k)≠0 
        𝜎(k)=𝜎(k)
        ∃m.𝜎(k)=𝜎(m)
      }
      𝜎(k)≠0 ⇒ (∃m.𝜎(k)=𝜎(m))     
    }
    ➤ "It would be nice to avoid the need for ∀ and ⇒ in this proof."
    ➤ "But 𝜆P(k) only matches expressions"
    ∀j. j≠0 ⇒ (∃m.j=𝜎(m))
    n≠0 ⇒ (∃m.n=𝜎(m))
    ∃m.n=𝜎(m)
  }
}

{
  ➤ "Theorem 7.3: n≠𝜎(n)"
  { ¬n=𝜎(n) }

  ➤ "Proof:"
  Proof:
  { 
    ➤ "Base Case:"
    ¬0=𝜎(0)
    
    ➤ "Inductive step:"
    { :Let k be such that ¬k=𝜎(k)
      { :𝜎(k)=𝜎(𝜎(k))
        k=𝜎(k)
        →←
      }
      ¬𝜎(k)=𝜎(𝜎(k))
    }
    ∀j.¬j=𝜎(j)
    ¬n=𝜎(n)
  }
}


➤ "Theorem 7.4: 𝜎(n)=n+1"
Theorem: { 𝜎(n)=n+1 }

{
  ➤ "Proof:"
  Proof:
  { 
    𝜎(n) = 𝜎(n+0)
         = n+𝜎(0)
         = n+1
  }
}

{
  ➤ "Theorem 7.5: Associativity of Addition"
  { (m+n)+p=m+(n+p) }

  ➤ "Proof: (by induction on p for arbitrary m,n)"
  Proof:
  { 
    ➤ "Base case:"
    (m+n)+0 = m+n
            = m+(n+0) 
            
    ➤ "Inductive step:"
    { :Let k be such that (m+n)+k=m+(n+k)
      
      (m+n)+𝜎(k) = 𝜎((m+n)+k) 
                 = 𝜎(m+(n+k)) 
                 = m+𝜎(n+k) 
                 = m+(n+𝜎(k)) 

    }
    ➤ "Therefore, by induction"
    ∀p.(m+n)+p=m+(n+p)
  }
}

➤ "We assume these for now."
Rules:
{
  ➤ "Theorem 7.6: (Left) Additive identity"
  :{ 0+m=m }
}

➤ "Theorem 7.7 (alt): (nicer alternative to Theorem 7.7)"
Theorem: { 𝜎(m+n)=𝜎(m)+n }

{
  Proof:
  {
    ➤ "Proof: (by induction on n)"
    ➤ 
    ➤ "Base case:" 
    𝜎(m+0) = 𝜎(m)
           = 𝜎(m)+0
    ➤ "Inductive Step"
    { :Let k be such that 𝜎(m+k)=𝜎(m)+k
      𝜎(m+𝜎(k)) = 𝜎(𝜎(m+k))
                = 𝜎(𝜎(m)+k)
                = 𝜎(m)+𝜎(k)
    }
    ➤ "So by induction,"
    ∀n.𝜎(m+n)=𝜎(m)+n
  }
}

➤ "Theorem 7.8 Commutativity of Addition."
Theorem:  { m+n = n+m }

{
  ➤ "Proof: (by induction on n)"
  Proof:
  { 
    ➤ "Base case"
    m+0 = m
        = 0+m
    ➤ "Inductive step."
    { :Let k be such that m+k=k+m
      
      m+𝜎(k) = 𝜎(m+k)
             = 𝜎(k+m)
             = 𝜎(k)+m

    }
    ∀n.m+n=n+m
  }    
}

➤ "Theorem 7.10 Cancellation Law for Addition."
{ :m+p=n+p  m=n }

{
  ➤ "Proof: (by induction on p)"
  Proof:
  { 
    ➤ "Base case"
    { :m+0 = n+0
        m = m+0
          = n+0
          = n
    }
    m+0=n+0 ⇒ m = n     
    ➤ "Inductive step."
    { :Let k be such that m+k=n+k ⇒ m=n
      { :m+𝜎(k)=n+𝜎(k)
        𝜎(m+k) = m+𝜎(k)
               = n+𝜎(k)
               = 𝜎(n+k)
        m+k=n+k
        m=n
      }
      m+𝜎(k)=n+𝜎(k) ⇒ m=n
    }
    ➤ "Therefore, by induction"
    ∀p.m+p=n+p⇒m=n
    m+p=n+p⇒m=n
    {:m+p=n+p
      m=n
    }
  }    
}

➤ "Theorem 7.12: Multiplicative identity."
Theorem: { n⋅1=n  1⋅n=n }

{
  ➤ "Proof:"
  Proof:
  { 
    ➤ "The first part is straightforward"
    n⋅1 = n⋅𝜎(0)
        = n+n⋅0
        = n+0
        =n

    ➤ "For the second part we need induction."
    ➤ "Base case."
    1⋅0 = 0
    
    ➤ "Inductive step."
    { :Let k be such that 1⋅k=k
      
       1⋅𝜎(k) = 1+1⋅k
              = 1+k
              = k+1
              = 𝜎(k)

    }
    ➤ "Therefore, by induction"
    ∀n.1⋅n=n
    
  }
}


{
  ➤ "Theorem 7.23: ≤ is reflexive."
  { n≤n }

  ➤ "Proof:"
  Proof:
  { n+0 = n
    ∃k. n+k = n
  }

}

{
  ➤ "Theorem 7.35: Every number divides itself."
  { 1 | n  n | n }
  ➤ "Proof:"
  Proof:
  { 
    ➤ "These follow from Thm 7.12"
    1⋅n = n = n⋅1
    ∃k.n=1⋅k
    1 | n
    ∃k.n=n⋅k
    n | n
  }
}

{
  ➤ "Theorem 7.36: Divisors are smaller."
  { :n≠0 :m|n  m≤n }
  
  ➤ "Proof:"
  Proof:
  { :n≠0 :m|n
    n=m⋅k for some k

    { :k=0
      n = m⋅k = m⋅0 = 0
      →← 
    }
    k≠0

    ∃m.k=𝜎(m)
    k=𝜎(j) for some j
    
    n = m⋅k
      = m⋅𝜎(j)
      = m + m⋅j
    
    ∃d.m+d=n
    m≤n
  }
}


// end of all
} ✔︎
