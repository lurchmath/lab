include libs/Number Theory
////////////////////////////////////////////////////////////////////
// Peano Test Proofs
// 

➤ "Math 299 Midterm Exam Proofs 2023"

{ // wrap them all
  
➤ "Math 299 Midterm 2023 #10"

{
  ➤ "Theorem: P ⇒ (P ⇔ P or (¬P and P))"
  { P ⇒ (P ⇔ P or (¬P and P)) }
  
  ➤ "Proof:"
  Proof:
  { :P
    { :P P or (¬P and P) }
    { :P or (¬P and P) 
      {:¬P and P  P}  
    } 
    (P ⇔ P or (¬P and P))
  }
}
  
➤ "Math 299 Midterm 2023 #11"

{
  ➤ "Theorem: ((P or Q) and (Q⇒R)) and ¬P ⇒ R"
  { ((P or Q) and (Q⇒R)) and ¬P ⇒ R }

  ➤ "Proof:"
  Proof:
  { :((P or Q) and (Q⇒R)) and ¬P
    (P or Q) and (Q⇒R)
    ¬P
    Q⇒R
    P or Q
    { :P { :¬R →← } R }
    { :Q R }
    R by cases
  }
  
}

➤ "Math 299 Midterm 2023 #12"

{
  ➤ "Theorem x=y and not y=z ⇒ not x=z"
  { x=y and ¬y=z ⇒ ¬x=z }

  ➤ "Proof:"
  Proof:
  { 
    { :x=y and ¬y=z
      ¬y=z
      { :x=z
        y=z because { :x=y :x=z y=z }
        →←
      }
      ¬x=z
    }
    x=y and ¬y=z ⇒ ¬x=z
  }
}

➤ "Math 299 Midterm 2023 #15 (Fortune Cookie)"
{
  ➤ "If all who give love receive it,"
  ➤ "then only one who does not love may be unloved."
  {  (∀x.(∃y.x loves y)⇒(∃z.z loves x)) ⇒ (∀x.(∀y.¬y loves x)⇒(∀y.¬x loves y)) }

  ➤ "Proof:"
  Proof:
  { :∀x. (∃y.x loves y) ⇒ (∃z.z loves x)
    { :Let s
      { :∀y.¬y loves s
        { :Let t
          { :s loves t
            ∃y.s loves y
            (∃y.s loves y) ⇒ (∃y.y loves s)
            ∃y.y loves s
            c loves s for some c
            ¬c loves s
            →←
          } 
          ¬s loves t
        } 
        ∀y.¬s loves y 
      }
      (∀y.¬y loves s) ⇒ (∀y.¬s loves y)
    }
    ∀x.(∀y.¬y loves x) ⇒ (∀y.¬x loves y)
  }
}

// end of all
} ✔︎
