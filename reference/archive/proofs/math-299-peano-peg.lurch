////////////////////////////////////////////////////////////////////
// Math 299 Peano proofs in peggy notation
//
{
  // "Theorem 7.8: Commutativity of Addition"

  ➤ "First we prove a Lemma"
  ➤ " "
  ➤ "Lemma: 0+n=n"
  Lemma: { 0+n=n }
  ➤ " "
  ➤ "Proof:"
  {
    { 
      // base case
      0+0=0
      // inductive step
      { :Let k be such that 0+k=k
        
        // 0+𝜎(k)=𝜎(0+k)
        //       =𝜎(k)
        
        0+𝜎(k)=𝜎(0+k)
        Recall { :0+k=k :𝜎(0+k)=𝜎(0+k) 𝜎(0+k)=𝜎(k) }
        𝜎(0+k)=𝜎(k)
        Recall { :0+𝜎(k)=𝜎(0+k) :𝜎(0+k)=𝜎(k) 0+𝜎(k)=𝜎(k) } 
        0+𝜎(k)=𝜎(k)
        
      }
      ∀m.0+m=m
      0+n=n
    }
  }
  
  // We are also allowed to use the following previous theorems for this problem
  ➤ "Theorem 7.4: Alternate definition of 𝜎"
  Thm: :{ 𝜎(n)=n+1 }
  ➤ "Theorem 7.5: Associativity of addition"
  Thm: :{ m+(n+p)=(m+n)+p }
  ➤ "Theorem 7.7: Commutativity of Adding 1"
  Thm: :{ 1+n=n+1 }
  ➤ " "
  ➤ "Now we can prove the main theorem"
  ➤ " "
  ➤ "Theorem 7.8: Addition is commutative."
  { m+n = n+m }
  ➤ " "
  ➤ "Proof:"
  { 
    ➤ "We will proceed by induction on n for arbitrary m."
    { :Let m 
      ➤ "Base case."
      Recall { :0+m=m :m+0=m m+0=0+m }
      m+0=0+m 
      ➤ "Inductive step."
      { :Let k be such that m+k=k+m

        // m+𝜎(k)=𝜎(m+k)
        //       =𝜎(k+m)
        //       =k+𝜎(m)
        //       =k+(m+1)
        //       =k+(1+m)
        //       =(k+1)+m
        //       =𝜎(k)+m
        
        m+𝜎(k)=𝜎(m+k)
        Recall { :m+k=k+m :𝜎(m+k)=𝜎(m+k) 𝜎(m+k)=𝜎(k+m) }
        // 𝜎(m+k)=𝜎(k+m)
        Recall { :𝜎(m+k)=𝜎(k+m) :m+𝜎(k)=𝜎(m+k)  m+𝜎(k)=𝜎(k+m) }
        // m+𝜎(k)=𝜎(k+m)
        Recall { :k+𝜎(m)=𝜎(k+m) :m+𝜎(k)=𝜎(k+m) m+𝜎(k)=k+𝜎(m) }
        // m+𝜎(k)=k+𝜎(m)
        Recall { :𝜎(m)=m+1 :m+𝜎(k)=k+𝜎(m) m+𝜎(k)=k+(m+1) }
        // m+𝜎(k)=k+(m+1)
        Recall { :1+m=m+1 :m+𝜎(k)=k+(m+1) m+𝜎(k)=k+(1+m) }
        // m+𝜎(k)=k+(1+m)
        Recall { :k+(1+m)=(k+1)+m :m+𝜎(k)=k+(1+m) m+𝜎(k)=(k+1)+m }
        // m+𝜎(k)=(k+1)+m
        Recall { :𝜎(k)=k+1 :m+𝜎(k)=(k+1)+m m+𝜎(k)=𝜎(k)+m }
        m+𝜎(k)=𝜎(k)+m
      }
      ∀n.m+n=n+m
    }
    ∀m.∀n.m+n=n+m
  }
  ∀n.m+n=n+m
  m+n=n+m
}