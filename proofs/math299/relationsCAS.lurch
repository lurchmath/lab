include libs/Relations
////////////////////////////////////////////////////////////////////
// Relation Test Proofs (with CAS)
// 

➤ "Math 299 Relations Homework Proofs 2023"

{ // wrap them all

{
	➤ "Assignment 22 - Problem 4"
	
	{
		➤ "Theorem 10.65: Congruence mod m is an equivalence relation."
		
		// Theorem: { '~' is an equivalence relation }
	  ➤ "Proof:"
	  { 
			➤ "Define '~' to be congruence mod m."
			Let m
			Definition: :{ x~y ≡ x ≈ y mod m } 
			➤ "Reflexive:"
			{ Let x
        
        x-x = 0
				    = m⋅0
			  
				∃d. x-x = m⋅d		
			  m | x-x
				x ≈ x mod m
				x ~ x
			}
	  	'~' is reflexive
			
			➤ "Symmetric:"
			{ Let x,y such that x~y
        
				x ≈ y mod m
			  m | x-y
        x-y = m⋅k for some k
        
				y-x = m⋅(-k) by CAS("test(y-x-m*(-k) = -(x-y-m*k),1,0)")
				
				∃d.y-x=m⋅d
				m | y-x
				y ≈ x mod m
				y ~ x
			}
			'~' is symmetric
			{ Let x,y,z such that x~y and y~z
				x~y 
				y~z
        x ≈ y mod m
        y ≈ z mod m
				m | x-y
				m | y-z
				// TODO: why does changing i to k on the next line and below cause a problem?
				x-y = m⋅i for some i
				y-z = m⋅j for some j

				x-z = m⋅(i+j) by CAS("test(x-z-m*(i+j) = x-y-m*i + y-z-m*j,1,0)")

				∃d.x-z=m⋅d
				m | x-z
				x ≈ z mod m
				x~z
			}
			'~' is transitive
	  }
  }
}

// end of all
} 
